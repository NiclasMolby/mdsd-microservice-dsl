grammar dk.sdu.mdsd.micro_lang.MicroLang with org.eclipse.xtext.common.Terminals

generate microLang "http://www.sdu.dk/mdsd/micro_lang/MicroLang"

Model:
	elements+=Element*
;

Element:
	Template | Microservice
;

Template:
	'template' name=ID ('(' parameters+=Parameter (',' parameters+=Parameter)* ')')?
		declarations+=Declaration*
;
	
Microservice:
	'microservice' name=ID location=Location
		declarations+=Declaration*
;

Location:
	'@' location=ID ':' port=INT
;

Declaration:
	Uses | Implements | Endpoint
;

Uses:
	'uses' target=[Microservice]
;

Implements:
	'implements' target=[Template] ('(' arguments+=ID (',' arguments+=ID)* ')')?
;

Endpoint:
	pathParts+=PathPart+ operations+=Operation+
;

PathPart:
	NormalPath | ParameterPath
;

NormalPath:
	'/' name=ID
;

ParameterPath:
	'/' '{' parameter=TypedParameter '}'
;

Operation:
	method=Method statements+=Statement*
;

Method:
	({TemplatedMethod} '{' name=ID '}') | name=('GET' | 'POST' | 'PUT' | 'DELETE' | 'OPTIONS')
;

Statement:
	TypedParameter | Require | Return
;

Parameter:
	({TemplatedParameter} '{' name=ID '}') | name=ID 
;

TypedParameter:
	type=Type parameter=Parameter
;

Require:
	'require' logic=Logic
;

Logic:
	'l'
	//boolean logic omitted for brevity
;

Return:
	'return' type=Type
;

Type:
	({TemplatedType} '{' name=ID '}') | name=('bool' | 'char' | 'string' | 'int' | 'long' | 'float' | 'double') ('['']')*
;