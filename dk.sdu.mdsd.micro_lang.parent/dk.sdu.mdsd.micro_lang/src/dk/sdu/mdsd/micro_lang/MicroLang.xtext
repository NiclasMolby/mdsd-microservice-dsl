grammar dk.sdu.mdsd.micro_lang.MicroLang with org.eclipse.xtext.common.Terminals

generate microLang "http://www.sdu.dk/mdsd/micro_lang/MicroLang"

Model:
	microservices+=Microservice*
;
	
Microservice:
	'microservice' name=ID location=Location '{'
		declarations+=Declaration*
	'}'
;

Location:
	'@' location=ID ':' port=INT
;

Declaration:
	Uses | Endpoint
;

Uses:
	'uses' target=[Microservice]
;

Endpoint:
	method=Method pathParts+=PathPart+ ('{'
		statements+=Statement*
	'}')?
;

Method:
	'GET' | 'POST' | 'PUT' | 'DELETE' | 'OPTIONS'
;

PathPart:
	NormalPath | ParameterPath
;

NormalPath:
	path = NormalPathText
;

ParameterPath:
	path = ParameterPathText
;

NormalPathText:
	'/' (ID)?
;

ParameterPathText:
	'/' '{' ID '}'
;

Statement:
	Parameter | Return
;

Parameter:
	type=Type name=ID
;

Return:
	'return' type=Type
;

Type:
	('bool' | 'char' | 'string' | 'int' | 'long' | 'float' | 'double') ('['']')?
;
