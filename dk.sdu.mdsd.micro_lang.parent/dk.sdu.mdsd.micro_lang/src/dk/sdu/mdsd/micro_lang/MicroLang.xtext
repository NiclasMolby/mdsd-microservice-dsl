grammar dk.sdu.mdsd.micro_lang.MicroLang with org.eclipse.xtext.common.Terminals

generate microLang "http://www.sdu.dk/mdsd/micro_lang/MicroLang"

Model:
	elements+=Element*
;

terminal BEGIN: 'synthetic:BEGIN';
terminal END: 'synthetic:END';

Element:
	Template | Microservice
;

Template:
	'template' name=ID ('(' parameters+=Parameter (',' parameters+=Parameter)* ')')?
	(BEGIN
		declarations+=Declaration*
	END)*
;

Microservice:
	'microservice' name=ID location=Location
	(BEGIN
		declarations+=Declaration*
	END)*
;

Location:
	'@' host=STRING ':' port=INT
;

Declaration:
	Uses | Implements | Endpoint
;

Uses:
	'uses' target=[Microservice]
;

Implements:
	'implements' target=[Template] ('(' arguments+=STRING (',' arguments+=STRING)* ')')?
;

Endpoint:
	pathParts+=PathPart+
	BEGIN
		operations+=Operation+
	END
;

PathPart:
	NormalPath | ParameterPath
;

NormalPath:
	'/' (name=ID | '{' target=[Parameter] '}')
;

ParameterPath:
	'/' '{' parameter=TypedParameter '}'
;

Operation:
	method=Method
	(BEGIN
		statements+=Statement*
	END)*
;

Method:
	name=('GET' | 'POST' | 'PUT' | 'DELETE' | 'OPTIONS') | '{' target=[Parameter] '}'
;

Statement:
	TypedParameter | Require | Return
;

Parameter:
	name=ID
;

TypedParameter:
	type=Type (name=ID | '{' target=[Parameter] '}')
;

Require:
	'require' logic=Logic
;

Logic:
	'l'
	//boolean logic omitted for brevity
;

Return:
	'return' type=Type
;

Type:
	name=('bool' | 'string' | 'int' | 'double') arrays+=(Array)* | '{' target=[Parameter] '}'
;

Array:
	'[' ']'
;